/**
 * HacX 2.0 - ZScript
 *
 * Weapon: Cryogun
 */

class HacxCryogun : HacxWeapon replaces SuperShotgun
{
  Default
  {
    Weapon.SelectionOrder 400;
    Weapon.UpSound "weapons/cryoup";
    Weapon.AmmoUse 2;
    Weapon.AmmoGive 8;
    weapon.ammotype "HacxCartridges";
    inventory.pickupmessage "$GOTCRYOGUN";
    obituary "$OB_MPCRYOGUN";
    Tag "$TAG_CRYOGUN";
  }

  states
  {
    Spawn:
      CRYO A -1;
      Stop;
    Ready:
      CRYG A 1 A_WeaponReady();
      loop;
    Idle:
      CRYG A 0;
      Goto IdleWait;
    Deselect:
      CRYG A 0 A_Lower();
      CRYG A 1 A_Lower();
      loop;
    Select:
      CRYG A 0 A_Raise();
      CRYG A 1 A_Raise();
      loop;
    Fire:
      CRYG A 2;
      CRYG A 0 A_GunFlash();
      CRYG A 0 A_PlaySound("weapons/cryofire", CHAN_WEAPON);
      CRYG AAAAA 0 A_FireProjectile("CryoShot",0.1*random(-80,80),0,0,0,1,0.1*random(-20,20));
      CRYG AAAA 0  A_FireProjectile("CryoShot",0.1*random(-65,65),0,0,0,1,0.1*random(-30,30));
      CRYG AAAA 0  A_FireProjectile("CryoShot",0.1*random(-50,50),0,0,0,1,0.1*random(-40,40));
      CRYG AAAA 0  A_FireProjectile("CryoShot",0.1*random(-35,35),0,0,0,1,0.1*random(-50,50));
      CRYG A 4     A_FireProjectile("CryoShot",0.1*random(-35,35),1,0,0,1,0.1*random(-20,20));
      CRYG B 3;
      CRYG C 3 A_CheckReload();
      CRYG D 4 A_PlaySound("weapons/uzireload", CHAN_WEAPON);
      CRYG E 5;
      CRYG F 4 A_PlaySound("weapons/cryohit", CHAN_WEAPON);
      CRYG G 4;
      CRYG H 0 A_PlaySound("weapons/cryocool", CHAN_WEAPON);
      CRYG H 3 A_ReFire();
      goto Ready;
    Flash:
      CRYG I 2 BRIGHT A_Light1();
      CRYG J 3 BRIGHT A_Light0();
      stop;
    AltFire:
      CRYK A 0;
      Goto Kick;
  }
}

class CryoShot : FastProjectile
{
  Default
  {
    DamageFunction (5*random(1,3)); // same as standard Doom hitscan
    Radius 8;
    Height 8;
    Speed 120;
    RenderStyle "Add";
    SeeSound "";
    DeathSound "weapons/cryohit";
    Obituary "$OB_MPCRYOGUN";
    Decal "CryoScorch";
  }

  States
  {
    Spawn:
      CPRJ A 1 bright A_SpawnItemEx("CryoTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0);
      Loop;
    Death:
      CPUF ABCDEFG 3 bright;
      Stop;
  }
}

class CryoTrail : HacxTrail
{
  Default
  {
    Alpha 0.25;
    Scale 0.5;
    Renderstyle "Add";
  }

  States
  {
    Spawn:
      CPUF CDCD 3 bright A_FadeOut(0.05);
      Stop;
  }
}
