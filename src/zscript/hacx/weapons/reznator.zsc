/**
 * HacX 2.0 - ZScript
 *
 * Weapon: HOIG Reznator
 */

class HacxReznator : HacxWeapon replaces Chainsaw
{
  Default
  {
    Weapon.Kickback 0;
    Weapon.SelectionOrder 2200;
    Weapon.UpSound "weapons/rezup";
    Weapon.ReadySound "weapons/rezidle";
    Inventory.PickupMessage "$GOTREZNATOR";
    Obituary "$OB_MPREZNATOR";
    Tag "$TAG_REZNATOR";
    +WEAPON.MELEEWEAPON;
  }

  States
  {
    Spawn:
      REZN A -1;
      Stop;
    Ready:
      REZG CD 4 A_WeaponReady();
      Loop;
    Idle:
      REZG D 2 A_WeaponReady(WRF_NOSECONDARY);
      REZG C 0 A_PlaySound("weapons/rezidle");
      REZG CD 4 A_WeaponReady(WRF_NOSECONDARY);
      Goto Ready;
    Deselect:
      REZG C 0 A_StopSound(CHAN_WEAPON);
      REZG C 0 A_PlaySound("weapons/rezdown", CHAN_AUTO);
    DeselectLoop:
      REZG C 0 A_Lower();
      REZG C 1 A_Lower();
      Loop;
    Select:
      REZG C 0 A_Raise();
      REZG C 1 A_Raise();
      REZG C 0 A_Raise();
      REZG C 1 A_Raise();
      REZG C 0 A_Raise();
      REZG C 1 A_Raise();
      REZG C 0 A_Raise();
      REZG C 1 A_Raise();
      REZG C 0 A_Raise();
      REZG C 1 A_Raise();
      REZG D 0 A_Raise();
      REZG D 1 A_Raise();
      REZG D 0 A_Raise();
      REZG D 1 A_Raise();
      REZG D 2 A_WeaponReady();
      REZG CD 4 A_WeaponReady();
      Goto Ready;
    Fire:
      REZG A 0 A_GunFlash("LightDone", GFF_NOEXTCHANGE);
      REZG AB 3 bright A_Saw("weapons/rezfull","weapons/rezhit",2,"ReznatorPuff");
      REZG B 0 A_ReFire();
      goto Ready;
    AltFire:
      REZK A 0;
      Goto Kick;
  }
}

class ReznatorPuff : HacxPuff
{
  Default
  {
    +PUFFONACTORS;
    DamageType "Reznator";
    RenderStyle "Add";
    Alpha 0.8;
    VSpeed 0;
  }
  States
  {
    Spawn:
    Melee:
      RZAP ACEGH 2 bright;
      Stop;
  }
}
