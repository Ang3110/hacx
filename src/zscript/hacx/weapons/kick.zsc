/**
 * HacX 2.0 - ZScript
 *
 * Weapon: Fu Kick
 */

class HacxKick : HacxWeapon
{
  Default
  {
    Weapon.SelectionOrder 3700;
    Obituary "$OB_MPKICK";
    Tag "$TAG_KICK";
    +WEAPON.NOALERT;
    +WEAPON.WIMPY_WEAPON;
    +WEAPON.MELEEWEAPON;
  }

  States
  {
    Ready:
      KICK A 1 A_WeaponReady();
      Loop;
    Deselect:
      KICK A 0 A_Lower();
      KICK A 1 A_Lower();
      Goto Deselect+1;
    Select:
      KICK A 0 A_Raise();
      KICK A 1 A_Raise();
      Goto Select+1;
    AltFire:
    Fire:
      KICK B 3 A_PlaySound("weapons/grunt", CHAN_WEAPON);
      KICK C 3;
      KICK D 0 A_JumpIfInventory("PowerStrength",1,"KickSuper");
      KICK D 8 A_CustomPunch(4*random(1,10),1,0,"KickPuff",96);
    KickFinish:
      KICK C 3;
      KICK B 3;
      KICK A 0 A_ReFire();
      goto Ready;
    KickSuper:
      KICK D 8 A_CustomPunch(40*random(1,10),1,0,"KickPuff",96);
      goto KickFinish;
  }
}

class KickPuff : HacxPuff
{
  Default
  {
    +PUFFONACTORS;
    DamageType "Kick";
    SeeSound "weapons/kick";
    ProjectileKickBack 500;
  }
}
