/**
 * HacX 2.0 - ZScript
 *
 * Enemy: Buzzer / Stealth
 */

class HacxBuzzer : Actor replaces Demon
{
  Default
  {
    hitobituary "$OB_BUZZER";
    health 150;
    speed 12;
    radius 25;
    height 68;
    painchance 100;
    painchance "Reznator", 255;
    damagefactor "Reznator", 1.5;
    mass 600;
    seesound "buzzer/sight";
    attacksound "buzzer/attack";
    painsound "buzzer/pain";
    deathsound "buzzer/death";
    activesound "buzzer/active";
    MONSTER;
    +FLOORCLIP;
    +NOGRAVITY;
    +FLOAT;
    +NOBLOOD;
  }

  States
  {
    Spawn:
      BUZZ A 1 A_Look();
      loop;
    See:
      BUZZ AABBCCDD 2 A_Chase();
      loop;
    Melee:
      BUZZ E 4 A_FaceTarget();
      BUZZ F 4 Bright A_FaceTarget();
      BUZZ G 5 Bright A_SargAttack();
      BUZZ F 5 Bright;
      goto See;
    Pain:
      BUZZ H 2 bright;
      BUZZ H 8 bright A_Pain();
      goto See;
    Death:
      BUZZ I 5 bright A_SetTranslucent(1.0,0);
      BUZZ J 5 bright A_Scream();
      BUZZ K 5 bright A_SpawnProjectile("HacxBuzzerDamage", 32);
      BUZZ L 5 bright;
      BUZZ M 5 bright;
      BUZZ N -1 A_NoBlocking();
      stop;
    Death.Reznator:
      BUZZ I 0 bright A_SetTranslucent(1.0,0);
      BUZZ I 5 bright A_PlaySound("buzzer/disable");
      BUZZ N -1 A_NoBlocking();
      stop;
    Raise:
      BUZZ NMLKJI 5;
      goto See;
  }
}

class HacxBuzzerDamage : HacxProjectile
{
  Default
  {
    +THRUACTORS;
    Damage 0;
    Speed 0;
    RenderStyle "None";
  }

  States
  {
    Spawn:
      TNT1 A 0 BRIGHT;
    Death:
      TNT1 A 1 bright A_Explode(96,128,0);
      Stop;
  }
}

class HacxStealth : HacxBuzzer replaces Spectre
{
  Default
  {
    hitobituary "$OB_STEALTH";
    renderstyle "Stencil";
    stencilcolor "00 00 00";
    alpha 0.2;
    speed 8;
    health 30;
    painchance 250;
    seesound "";
    painsound "";
    activesound "";
  }
}
