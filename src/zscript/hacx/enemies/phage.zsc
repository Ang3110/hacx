/**
 * HacX 2.0 - ZScript
 *
 * Enemy: Phage
 */

class HacxPhage : Actor replaces Fatso
{
  Default
  {
    obituary "$OB_PHAGE";
    health 150;
    speed 22;
    radius 25;
    height 96;
    damage 5;
    meleerange 128;
    painchance 75;
    mass 150;
    seesound "phage/sight";
    painsound "phage/pain";
    deathsound "phage/death";
    activesound "phage/active";
    MONSTER;
    +FLOORCLIP;
  }

  States
  {
    Spawn:
      PHAG G 10 A_Look();
      loop;
    See:
    Chase:
      PHAG AABB 4 A_Chase();
      loop;
    Melee:
      PHAG D 10 A_FaceTarget();
      PHAG E 5 bright A_PlaySound("phage/melee");
      PHAG F 10 bright A_SkullAttack();
      PHAG D 10 A_Stop();
      goto Chase;
    Missile:
      PHAG G 0 A_PlaySound("phage/attack");
      PHAG H 10 A_FaceTarget();
      PHAG I 10 bright A_SpawnProjectile ("HacxPhageBall", 32.0, 0.0, 0);
      PHAG G 5 bright A_FaceTarget();
      PHAG H 5 A_FaceTarget();
      PHAG I 10 bright A_SpawnProjectile ("HacxPhageBall", 32.0, 0.0, 0);
      PHAG G 5 bright A_FaceTarget();
      PHAG H 5 A_FaceTarget();
      PHAG I 10 bright A_SpawnProjectile ("HacxPhageBall", 32.0, 0.0, 0);
      PHAG G 5 bright A_FaceTarget();
      goto Chase;
    Pain:
      PHAG J 3;
      PHAG J 3 A_Pain();
      goto Chase;
    Death:
      PHAG K 5;
      PHAG L 5 A_Scream();
      PHAG M 5 A_Fall();
      PHAG NOPQR 5;
    DeathLoop:
      PHAG S 18;
      PHAG T 6;
      goto DeathLoop;
  }
}

class HacxPhageBall : ArachnotronPlasma replaces ArachnotronPlasma
{
  Default
  {
    speed 16;
    height 4;
    damage 3;
    mass 100;
    seesound "misc/projectilefire";
    deathsound "misc/projectilehit";
    PROJECTILE;
    +RANDOMIZE;
  }

  States
  {
    Spawn:
      PHGB AB 4 BRIGHT;
      loop;
    Death:
      PHGX ABCDE 5 bright;
      stop;
  }
}
