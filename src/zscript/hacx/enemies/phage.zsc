/**
 * HacX 2.0 - ZScript
 *
 * Enemy: Phage
 */

class HacxPhage : Actor replaces Fatso
{
  Default
  {
    obituary "$OB_PHAGE";
    health 150;
    speed 22;
    radius 25;
    height 96;
    damage 5;
    meleerange 128;
    painchance 75;
    mass 150;
    seesound "phage/sight";
    painsound "phage/pain";
    deathsound "phage/death";
    activesound "phage/active";
    MONSTER;
    +FLOORCLIP;
  }

  States
  {
    Spawn:
      PHAG B 10 A_Look();
      loop;
    See:
    Chase:
      PHAG ABCD 4 A_Chase();
      loop;
    Melee:
      PHAG E 10 A_FaceTarget();
      PHAG F 5 bright A_PlaySound("phage/melee");
      PHAG G 10 bright A_SkullAttack();
      PHAG E 10 A_Stop();
      goto Chase;
    Missile:
      PHAG H 0 A_PlaySound("phage/attack");
      PHAG I 10 A_FaceTarget();
      PHAG J 10 bright A_SpawnProjectile ("HacxPhageBall", 32.0, 0.0, 0);
      PHAG H 5 bright A_FaceTarget();
      PHAG I 5 A_FaceTarget();
      PHAG J 10 bright A_SpawnProjectile ("HacxPhageBall", 32.0, 0.0, 0);
      PHAG H 5 bright A_FaceTarget();
      PHAG I 5 A_FaceTarget();
      PHAG J 10 bright A_SpawnProjectile ("HacxPhageBall", 32.0, 0.0, 0);
      PHAG H 5 bright A_FaceTarget();
      goto Chase;
    Pain:
      PHAG K 3;
      PHAG K 3 A_Pain();
      goto Chase;
    Death:
      PHAG L 5;
      PHAG M 5 A_Scream();
      PHAG N 5 A_Fall();
      PHAG OPQRS 5;
    DeathLoop:
      PHAG T 18;
      PHAG U 6;
      goto DeathLoop;
  }
}

class HacxPhageBall : ArachnotronPlasma replaces ArachnotronPlasma
{
  Default
  {
    speed 16;
    height 4;
    damage 3;
    mass 100;
    seesound "misc/projectilefire";
    deathsound "misc/projectilehit";
    PROJECTILE;
    +RANDOMIZE;
  }

  States
  {
    Spawn:
      PHGB AB 4 BRIGHT;
      loop;
    Death:
      PHGX ABCDE 5 bright;
      stop;
  }
}
