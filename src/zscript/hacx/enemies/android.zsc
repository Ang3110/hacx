/**
 * HacX 2.0 - ZScript
 *
 * Enemy: Android
 */

class HacxAndroid : Actor replaces ShotgunGuy
{
  Default
  {
    obituary "$OB_ANDROID";
    health 75;
    radius 21;
    height 70;
    mass 55;
    speed 16;
    painchance 20;
    painchance "Reznator", 255;
    damagefactor "Reznator", 1.5;
    seesound "android/sight";
    attacksound "android/attack";
    painsound "android/pain";
    deathsound "android/death";
    activesound "android/active";
    dropitem "HacxTazer", 256;
    scale 0.9;
    MONSTER;
    +FLOORCLIP;
    +NOBLOOD;
  }

  States
  {
    Spawn:
      ANDY E 10 A_Look();
      loop;
    See:
      ANDY AABBCCDD 3 A_Chase();
      loop;
    Missile:
      ANDY E 10 A_FaceTarget();
      ANDY E 0 A_PlaySound("android/attack");
      ANDY FF 0 A_SpawnProjectile("HacxAndroidShot",32,0,frandom(-22.5, 22.5));
      ANDY F 10 bright  A_SpawnProjectile("HacxAndroidShot",32,0,0);
      ANDY E 10; // A_CustomBulletAttack (22.5, 0, 3, random(1,5) * 3, "TazerPuff", 0, CBAF_NORANDOM)
      goto See;
    Pain:
      ANDY G 3;
      ANDY G 3 A_Pain();
      goto See;
    Death:
      ANDY H 9 bright;
      ANDY I 6 bright A_Scream();
      ANDY J 4 bright A_NoBlocking();
      ANDY K 4;
      ANDY L -1;
      stop;
    XDeath:
      ANDY H 0 bright A_Scream();
      ANDY H 10 bright A_PlaySound("android/gib",CHAN_AUTO);
      ANDY M 2 bright;
      ANDY N 4 bright A_PlaySound("misc/explode",CHAN_VOICE);
      ANDY O 0 bright A_Explode(128,128,0);
      ANDY O 4 bright A_NoBlocking();
      ANDY PQR 4 bright;
      ANDY ST 4;
      ANDY U -1;
      stop;
    Raise:
      ANDY LKJIH 5;
      goto See;
  }
}

class HacxAndroidShot : TazerShot
{
  Default
  {
    Speed 40;
    DamageFunction (5*random(1,3)); // same as standard Doom hitscan
    Seesound "";
    DeathSound "weapons/tazerhit";
    Decal "TazerScorch";
  }
  states
  {
    Spawn:
      TPUF AABBBAA 1 bright A_SpawnItemEx("TazerTrail2",0,0.5*random(-10,10),0.5*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0);
      loop;
    Death:
      TPUF CDE 2 bright;
      stop;
  }
}
