/**
 * HacX 2.0 - ZScript
 *
 * Enemy: Android
 */

class HacxAndroid : Actor replaces ShotgunGuy
{
  Default
  {
    obituary "$OB_ANDROID";
    health 75;
    radius 21;
    height 70;
    mass 55;
    speed 16;
    painchance 20;
    painchance "Reznator", 255;
    damagefactor "Reznator", 1.5;
    seesound "android/sight";
    attacksound "android/attack";
    painsound "android/pain";
    deathsound "android/death";
    activesound "android/active";
    dropitem "HacxTazer", 256;
    scale 0.9;
    MONSTER;
    +FLOORCLIP;
    +NOBLOOD;
  }

  States
  {
    Spawn:
      ANDY E 10 A_Look();
      loop;
    See:
      ANDY AABBCCDD 3 A_Chase();
      loop;
    Missile:
      ANDY E 10 A_FaceTarget();
      ANDY F 10 bright X_AndroidAttack();
      ANDY E 10;
      goto See;
    Pain:
      ANDY G 3;
      ANDY G 3 A_Pain();
      goto See;
    Death:
      ANDY H 9 bright;
      ANDY I 6 bright A_Scream();
      ANDY J 4 bright A_NoBlocking();
      ANDY K 4;
      ANDY L -1;
      stop;
    XDeath:
      ANDY H 0 bright A_Scream();
      ANDY H 10 bright A_PlaySound("android/gib",CHAN_AUTO);
      ANDY M 2 bright;
      ANDY N 4 bright A_PlaySound("misc/explode",CHAN_VOICE);
      ANDY O 0 bright A_Explode(128,128,0);
      ANDY O 4 bright A_NoBlocking();
      ANDY PQR 4 bright;
      ANDY ST 4;
      ANDY U -1;
      stop;
    Raise:
      ANDY LKJIH 5;
      goto See;
  }

  //
  // X_AndroidAttack
  //
  void X_AndroidAttack()
  {
    A_PlaySound("android/attack");
    A_CustomBulletAttack (22.5, 0, 3, random(1,5) * 3, "HacxTazerZap", 0, CBAF_NORANDOM);
  }
}
