/**
 * HacX 2.0 - ZScript
 *
 * Enemy: Monstruct
 */

class HacxMonstruct : Actor replaces ChaingunGuy
{
  Default
  {
    obituary "$OB_MONSTRUCT";
    health 400;
    speed 7;
    radius 35;
    height 87;
    painchance 50;
    mass 4000;
    seesound "monstruct/sight";
    painsound "monstruct/pain";
    deathsound "monstruct/death";
    activesound "monstruct/active";
    attacksound "weapons/pistol";
    damagefactor "Monstruct", 0.0;
    meleerange 128;
    MONSTER;
    +FLOORCLIP;
  }

  States
  {
    Spawn:
      MONS A 10 A_Look();
      loop;
    See:
      MONS AABBCCDD 4 A_Chase();
      loop;
    Missile:
      MONS G 0 A_JumpIfCloser(384,"Melee");
      MONS G 10 bright A_FaceTarget();// E: right, F: left
      MONS E 6 bright A_SpawnProjectile("HacxMonstructShot", 32, 24, 0);
      MONS F 6 bright A_SpawnProjectile("HacxMonstructShot", 32, -24, frandom(0,22.5));
      MONS G 6 bright A_FaceTarget();
      MONS E 6 bright A_SpawnProjectile("HacxMonstructShot", 32, 24, frandom(-22.5, 0));
      MONS F 6 bright A_SpawnProjectile("HacxMonstructShot", 32, -24, 0);
      MONS G 6;
      Goto See;
    Melee:
      MONS G 6 A_FaceTarget();
    MeleeLoop:
      MONS O 2 bright A_CustomBulletAttack(22.5, 0, 1, random(1,5) * 3, "HacxMonstructPuff", 0, CBAF_NORANDOM);
      MONS N 3;
      MONS O 0 A_JumpIfCloser (512, 1);
      Goto See;
      MONS O 0 A_CPosRefire();
      goto MeleeLoop;
    Pain:
      MONS D 5 A_Chase();
      goto See;
    Death:
      MONS H 5;
      MONS H 5 A_Scream();
      MONS I 10;
      MONS J 10 A_NoBlocking();
      MONS K 8;
      MONS L 6;
      MONS M -1;
      stop;
    Raise:
      MONS MLKJIH 5;
      goto See;
  }
}

class HacxMonstructShot : Actor replaces FatShot
{
  Default
  {
    speed 20;
    radius 6;
    height 8;
    damage 8;
    mass 100;
    seesound "misc/projectilefire";
    deathsound "misc/explode";
    damagetype "Monstruct";
    PROJECTILE;
    +RANDOMIZE;
  }

  States
  {
    Spawn:
      MONF AB 4 bright;
      loop;
    Death:
      TORE ABC 3 bright;
      TORE D 5 bright A_Explode(32,128,0);
      TORE E 5 bright;
      stop;
  }
}

class HacxMonstructPuff : HacxPuff
{
  Default
  {
    damagetype "Monstruct";
  }
}
